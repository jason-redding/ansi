import com.witcraft.coveragecompliance.DiffCoverageCompliancePlugin

buildscript {
    repositories {
        maven { url 'https://jitpack.io' }
    }
    dependencies {
        classpath('com.github.form-com.diff-coverage-gradle:diff-coverage:0.9.1') {
            exclude group: 'org.apache.logging.log4j', module: 'log4j-slf4j-impl'
            exclude group: 'org.slf4j', module: 'slf4j-api'
            exclude group: 'org.apache.logging.log4j', module: 'log4j-api'
            exclude group: 'org.apache.logging.log4j', module: 'log4j-core'
            exclude group: 'org.jetbrains.kotlin', module: 'kotlin-stdlib-jdk8'
        }
        constraints {
            classpath('org.slf4j:slf4j-api:1.7.36')
            classpath('org.apache.logging.log4j:log4j-slf4j-impl:2.17.2') {
                because 'version 2.11.0 pulled in from "diff-coverage" has associated CVEs'
            }
            classpath('org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.6.20-RC2') {
                because 'version 1.4.31 pulled in from "diff-coverage" has associated CVEs'
            }
        }
    }
}

plugins {
    id 'groovy'
    id 'java'
    id 'org.springframework.boot' version '2.6.6'
    id 'java-library-conventions'
    id 'jacoco-conventions'
}

group 'com.witcraft'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.apache.groovy:groovy:4.0.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
}

apply plugin: DiffCoverageCompliancePlugin

test {
    finalizedBy jacocoTestReport
    useJUnitPlatform()
}

compileJava {
    options.compilerArgs.add('-Xlint:unchecked')
}